plugins {
    id 'java'
    id 'application'
    id "com.github.breadmoirai.github-release" version "2.2.12"
}

group 'com.clouds42'
version '1.0-SNAPSHOT'

sourceCompatibility = 11

repositories {
    mavenCentral()
}

configurations {
    providedCompile
}

dependencies {
    def output = new ByteArrayOutputStream()
    exec {
        commandLine 'powershell', 'ring', 'edt', 'locations', 'list'
        standardOutput = output
    }
    def edtLocation = output.toString().trim().split("\n")[0].trim()
    println 'EDT location: ' + edtLocation
    compile group: 'info.picocli', name: 'picocli', version: '4.2.0'
    compile group: 'com.opencsv', name: 'opencsv', version: '5.1'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '2.0.0-alpha1'
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '2.0.0-alpha1'
    providedCompile fileTree(edtLocation + '/plugins/') { include '*.jar' }
}

compileJava {
    sourceSets.main.compileClasspath += configurations.providedCompile
}

run {
    classpath += configurations.providedCompile
}

startScripts {
  unixStartScriptGenerator.template = resources.text.fromFile(project.getRootDir().getAbsolutePath() + '/scripts/unixStartScript.txt')
  windowsStartScriptGenerator.template = resources.text.fromFile(project.getRootDir().getAbsolutePath() + '/scripts/windowsStartScript.txt')
}

application {
    mainClassName = 'com.clouds42.Coverage41C'
}

githubRelease {
    token findProperty('github.token')
    owner "proDOOMman"
    repo "Coverage41C"
    targetCommitish "master"
    releaseAssets.from(distZip.archiveFile)
    dryRun true
}
